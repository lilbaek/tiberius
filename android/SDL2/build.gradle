apply plugin: 'com.android.library'

def findLibrary(String prefix) {
    def extPath = project.file("../../ext/SDL2")
    def files = extPath.list({f, name -> name.startsWith("$prefix-")})
    if (files.length == 0) {
        throw new IllegalStateException("$prefix source not found, please extract the source to a subdirectory named '$prefix-2.0.*' in: " + extPath.absolutePath)
    } else if (files.length > 1) {
        throw new IllegalStateException("Multiple $prefix source folders found, please make sure there is only one folder named '$prefix-2.0.*' in: " + extPath.absolutePath)
    }
    return new File(extPath, files[0])
}

task findSDL2 {
    def path = findLibrary("SDL2")
    // Copy SDL Android sources, since Android Studio no longer
    // appears to recognize source sets outside the project root
    copy {
        from new File(path, "android-project/app/src/main/java")
        into new File(projectDir, "src/main/java")
    }
    ext.sourcePath = path
    ext.includePath = new File(path, "include")
}

task findSDL2Mixer {
    def path = findLibrary("SDL2_mixer")
    ext.sourcePath = path
    ext.includePath = new File(path, "include")
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"
    ndkVersion "25.1.8937393"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            ndkBuild {
                arguments 'SUPPORT_FLAC=false',
                        'SUPPORT_OGG=false',
                        'SUPPORT_MOD_MODPLUG=false',
                        'SUPPORT_MID_TIMIDITY=false'
            }
        }
    }
    buildFeatures {
        prefabPublishing true
    }
    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path '../../ext/SDL2/Android.mk'
        }
    }
    prefab {
        SDL2 {
            headers "${findSDL2.includePath}"
        }
        SDL2_mixer {
            headers "${findSDL2Mixer.includePath}"
        }
    }
    namespace 'com.github.Keriew.augustus.sdl2'
}

repositories {
    google()
    mavenCentral()
}

// Workaround for issue: https://issuetracker.google.com/issues/222811915
// Create prefab dir to prevent failing Gradle Sync in Android Studio.
// Sync will throw a warning (instead of error) but the project will build normally.
mkdir "$buildDir/intermediates/prefab_package_configuration/debug/prefab"
